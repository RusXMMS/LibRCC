dnl Process this file with autoconf to produce a configure script.
AC_INIT(src/librcc.c)
AC_CONFIG_HEADERS(config.h)

PACKAGE=librcc
LIBRCC_VERSION_MAJOR=`cat VERSION | sed -e s/CVS// | cut -d . -f 1 | sed -e s/^$/0/`
LIBRCC_VERSION_MINOR=`cat VERSION | sed -e s/CVS// | cut -d . -f 2 | sed -e s/^$/0/`
LIBRCC_VERSION_SUBMINOR=`cat VERSION | sed -e s/CVS// | cut -d . -f 3 | sed -e s/^$/0/`
LIBRCC_VERSION=$LIBRCC_VERSION_MAJOR.$LIBRCC_VERSION_MINOR.$LIBRCC_VERSION_SUBMINOR
VERSION=$LIBRCC_VERSION
LIBRCC_VERSION_INFO=`echo $LIBRCC_VERSION | awk -F. '{ printf "%d:%d:%d", $1+$2, $3, $2 }'`

AC_SUBST(LIBRCC_VERSION)
AC_SUBST(LIBRCC_VERSION_MAJOR)
AC_SUBST(LIBRCC_VERSION_MINOR)
AC_SUBST(LIBRCC_VERSION_SUBMINOR)
AC_SUBST(LIBRCC_VERSION_INFO)

LIBRCC_CVS=`cat VERSION | sed -e s/.*CVS.*/CVS/`
if test "x$LIBRCC_CVS" = "x"; then
LIBRCC_CVS=0
LIBRCC_CVS_DATE=0
else 
LIBRCC_CVS=1
LIBRCC_CVS_DATE=`date +%y%m%d.%H`
fi
AC_SUBST(LIBRCC_CVS)
AC_SUBST(LIBRCC_CVS_DATE)

AM_INIT_AUTOMAKE($PACKAGE, $VERSION)

AC_ARG_ENABLE( force-dynamic-engines,
    [  --enable-force-dynamic-engines	force usage of dynamic engines],,
    enable_force_dynamic_engines="no")

AC_PROG_CC
AC_PROG_INSTALL
AM_PROG_LIBTOOL

AC_PATH_PROG(RM, rm, /bin/rm)
AC_PATH_PROG(MV, mv, /bin/mv)
AC_PATH_PROG(TAR, tar, /bin/tar)

dnl Checks for programs.

dnl Checks for header files.
AC_CHECK_HEADERS(iconv.h,, [AC_MSG_ERROR(Missing iconv header)])
AC_CHECK_HEADERS(mntent.h pwd.h sys/types.h sys/stat.h unistd.h)

AC_TRY_COMPILE([#include <langinfo.h>],
    [char *codeset = nl_langinfo (CODESET);],
    [AC_DEFINE(HAVE_CODESET,,[Define if nl_langinfo(CODESET) is available.])]
)
    
dnl Checks for libraries.

AC_PATH_PROG(XML2_CONFIG, xml2-config, no)
if test $XML2_CONFIG = no; then
    AC_MSG_ERROR(LibXML2 is required)
fi
XML_LIBS="\`xml2-config --libs\`"
XML_INCLUDES="\`xml2-config --cflags\`"
AC_SUBST(XML_LIBS)
AC_SUBST(XML_INCLUDES)

AC_PATH_PROG(GTK_CONFIG, gtk-config, no)
AM_CONDITIONAL(HAVE_GTK, [ test $GTK_CONFIG != no ])
if test $GTK_CONFIG = no; then
    GTK_LIBS=""
    GTK_INCLUDES=""
    HAVE_GTK=no
else
    GTK_LIBS="\`gtk-config --libs\`"
    GTK_INCLUDES="\`gtk-config --cflags\`"
    HAVE_GTK=yes
fi
AC_SUBST(GTK_LIBS)
AC_SUBST(GTK_INCLUDES)

AC_CHECK_HEADER(dlfcn.h, [AC_CHECK_LIB(dl, dlopen, [
    AC_DEFINE(HAVE_DLOPEN,1,[Defines if dlopen is available])
    DLOPEN_LIBS="-ldl"
    DLOPEN_INCLUDES=""
    HAVE_DLOPEN=yes
],[
    DLOPEN_LIBS=""
    DLOPEN_INCLUDES=""
    HAVE_DLOPEN=no
])])

if test $enable_force_dynamic_engines = yes; then
    RCD_LIBS=""
    RCD_INCLUDES=""
    HAVE_RCD=no
    ENCA_LIBS=""
    ENCA_INCLUDES=""
    HAVE_ENCA=no
else
AC_CHECK_HEADER(librcd.h, [AC_CHECK_LIB(rcd, rcdGetRussianCharset, [
	AC_DEFINE(HAVE_RCD,1,[Defines if libRCD is available])
	RCD_LIBS="-lrcd"
	RCD_INCLUDES=""
	HAVE_RCD=yes
],[
	RCD_LIBS=""
	RCD_INCLUDES=""
	HAVE_RCD=no
])])
AC_CHECK_HEADER(enca.h, [AC_CHECK_LIB(enca, enca_analyse, [ 
	AC_DEFINE(HAVE_ENCA,1,[Defines if enca is available])
	ENCA_LIBS="-lenca"
	ENCA_INCLUDES=""
	HAVE_ENCA=yes
],[
	ENCA_LIBS=""
	ENCA_INCLUDES=""
        HAVE_ENCA=no
])])
fi

AC_SUBST(RCD_LIBS)
AC_SUBST(RCD_INCLUDES)
AC_SUBST(ENCA_LIBS)
AC_SUBST(ENCA_INCLUDES)

USE_DLOPEN=no
if test $HAVE_DLOPEN = yes; then
    if test $HAVE_ENCA = no; then
	HAVE_ENCA=dynamic
	USE_DLOPEN=yes
    fi
    if test $HAVE_RCD = no; then
	HAVE_RCD=dynamic
	USE_DLOPEN=yes
    fi
    
    if test $USE_DLOPEN = no; then
	DLOPEN_LIBS=""
	DLOPEN_INCLUDES=""
    fi
fi
AC_SUBST(DLOPEN_LIBS)
AC_SUBST(DLOPEN_INCLUDES)


AX_PATH_BDB([4],[
    BDB_LIBS="$BDB_LDFLAGS $BDB_LIBS"
    BDB_INCLUDES="$BDB_CPPFLAGS"
    HAVE_BDB=yes
],[
    BDB_LIBS=""
    BDB_INCLUDES=""
    HAVE_BDB=no
])
AC_SUBST(BDB_LIBS)
AC_SUBST(BDB_INCLUDES)


dnl Checks for typedefs, structures, and compiler characteristics.
AC_C_CONST

dnl Checks for library functions.
AC_CHECK_FUNCS(strcasecmp strncasecmp strdup)

AC_OUTPUT(src/Makefile ui/Makefile examples/Makefile Makefile librcc.spec)

echo ""
echo "Configuration:"
echo "  Dynamic Engine Loading Support:        $HAVE_DLOPEN"
echo "  Enca Charset Detection Support:        $HAVE_ENCA"
echo "  LibRCD Charset Detection Support:      $HAVE_RCD"
echo ""
echo "  Multilanguage support with DB4:        $HAVE_BDB"
echo ""
echo "User Interfaces:"
echo "  GTK User Interface:                    $HAVE_GTK"
echo ""
echo ""
echo ""
